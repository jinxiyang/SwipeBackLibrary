

/*****************************************************
*                :library:javadocJar                 *
*                :library:sourcesJar                 *
*                :library:install                    *
*                :library:bintrayUpload              *
******************************************************/


Properties properties = new Properties()
File projectPropertiesFile = project.file('deploy.properties')
if (projectPropertiesFile.exists()) {
    properties.load(projectPropertiesFile.newDataInputStream())
} else {
    println "Cannot find deploy.properties file in " + project.name + " folder"
}

Properties localProperties = new Properties()
File localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localProperties.load(localPropertiesFile.newDataInputStream())
} else {
    println "Cannot find local.properties file in " + project.name + " folder"
}


//发布的组别和版本号, 上传（bintrayUpload）时需要
group = properties.getProperty("project.groupId")
version = properties.getProperty("project.version")

apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}
task javadoc(type: Javadoc) {
    failOnError false
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

javadoc { //javadoc 采用 utf-8 编码否则会报“GBK的不可映射字符”错误
    options {
        encoding "UTF-8"
        charSet "UTF-8"
    }
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging properties.getProperty("project.packaging")
                groupId properties.getProperty("project.groupId")
                artifactId properties.getProperty("project.artifactId")
                version properties.getProperty("project.version")
                // Add your description here
                name properties.getProperty("project.name")
                url properties.getProperty("project.siteUrl")

                licenses {
                    license {
                        name properties.getProperty("license.name")
                        url properties.getProperty("license.url")
                    }
                }
                developers {
                    developer {
                        id properties.getProperty("developer.id")
                        name properties.getProperty("developer.name")
                        email properties.getProperty("developer.email")
                    }
                }
                scm {
                    connection properties.getProperty("project.gitUrl")
                    developerConnection properties.getProperty("project.gitUrl")
                    url properties.getProperty("project.siteUrl")
                }
            }
        }
    }
}


bintray {
    user = localProperties.getProperty("bintray.user")
    key = localProperties.getProperty("bintray.apikey")
    configurations = ['archives']
    pkg {
        repo = properties.getProperty("project.repo")
        name = properties.getProperty("project.name")
        websiteUrl = properties.getProperty("project.siteUrl")
        vcsUrl = properties.getProperty("project.gitUrl")
        licenses = ["Apache-2.0"]
        publish = true
    }
}